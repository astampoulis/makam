testsuite : type.

testcase : testsuite -> prop.

run_tests : testsuite -> prop.
run_tests : list testsuite -> prop.

run_tests (X : testsuite) :- run_tests [ X ] .

%extend testing_private.

handle_result : list clause -> list clause -> prop.
handle_result Successes [] <-
  length Successes Tests, tostring Tests TestsS,
  foldl string.append ""
  [ "\n\n=== TESTS SUCCESSFUL (", TestsS, " passed, 0 failed) ===\n\n" ] S,
  print_string S.

handle_result Successes Failures when not(eq Failures []) <-
  length Successes SuccessN, tostring SuccessN SuccessS,
  length Failures FailureN, tostring FailureN FailureS,
  foldl string.append ""
  [ "\n\n=== TESTS FAILED (", SuccessS, " passed, ", FailureS, " failed) ===\n\n" ] S,
  print_string S,
  failure.

handle_failure : clause -> prop.
handle_failure TestCase <-
  locget TestCase Loc,
  tostring Loc TestCaseS,
  foldl string.append ""
  [ "\n-- Test failure for testcase at ", TestCaseS, "\n" ] S,
  print_string S.

run_test : list testsuite -> list clause -> list clause -> prop -> list clause -> prop.
run_test Suites Successes Failures (testcase X) [] <- handle_result Successes Failures.
run_test Suites Successes Failures (testcase X) (TestCase :: TL) <-
  if (once(find (fun suite => clause.applies (testcase suite) TestCase) Suites _))
  then (
    if (once(clause.premise TestCase))
    then (run_test Suites (TestCase :: Successes) Failures (testcase X) TL)
    else (handle_failure TestCase, run_test Suites Successes (TestCase :: Failures) (testcase X) TL)
  )
  else (run_test Suites Successes Failures (testcase X) TL).

print_tests_aux : prop -> list clause -> prop.
print_tests_aux (testcase Suite) [].
print_tests_aux (testcase Suite) (HD :: TL) :-
 if (eq HD (clause (testcase Suite) Prem))
 then (print Prem)
 else success,
 print_tests_aux (testcase Suite) TL.

%end.

print_tests : testsuite -> prop.

print_tests Suite :-
  once(demand.aux_demand testing_private.print_tests_aux (testcase Suite)).

run_tests Suites <-
  if (refl.isunif Suites)
  then (eq SuitesStr "all")
  else (map tostring Suites (SuitesStrs : list string),
        foldl (pfun cur elm res => [X] string.append cur "- " X, string.append X elm res) "" SuitesStrs SuitesStr),
  foldl string.append ""
  [ "\n\n=== RUNNING TESTS (test suites: ", SuitesStr, ")\n" ] Str,
  print_string Str,
  once(demand.aux_demand (testing_private.run_test Suites [] []) (testcase X)).
