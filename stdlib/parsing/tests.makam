%extend peg.

parsing : testsuite. %testsuite parsing.

>> parse (bind anychar (fun a => seq (charclass " \n\t") (action a))) "a " Res ?
>> Yes:
>> Res := ("a", "").

>> parse (bind anychar (fun a => seq (charclass " \n\t") (action a))) "a !" Res ?
>> Yes:
>> Res := ("a", "!").

(* Actual bind *)
>> parse (bind anychar (fun a => exact a)) "aa" Res ?
>> Yes:
>> Res := ("a", "").

slow_many : peg A -> peg (list A).

rule (slow_many P) (bind P (fun hd => bind (slow_many P) (fun tl => action (hd :: tl)))).
rule (slow_many P) (action []).

>> parse (slow_many (exact "hi")) "hihi" Res ?
>> Yes:
>> Res := ([ "hi", "hi" ], "").

>> parse (seq ws (action unit)) "   return " Res ?
>> Yes:
>> Res := (unit, "return ").

>> parse eof "" Res ?
>> Yes:
>> Res := (unit, "").

>> parse eof "a" Res ?
>> Impossible.

token : peg A -> peg A.
rule (token P) (seq (option ws) P).

>> parse (seq (token (exact "return")) (token (exact "true"))) " return true " Res ?
>> Yes:
>> Res := ("true", " ").

ident : peg string.
identfirst, identrest : peg string.

rule identfirst (charclass "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZαβγδεζηθικμνξοπρστυφχψωΑΒΓΔΕΖΗΘΙΚΛΜΝΞΟΠΡΣΤΥΦΧΨΩάέήίύόώ_").
rule identrest (charclass "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZαβγδεζηθικλμνξοπρστυφχψωΑΒΓΔΕΖΗΘΙΚΛΜΝΞΟΠΡΣΤΥΦΧΨΩάέήίύόώ_1234567890'").

rule ident (bind identfirst (fun (hd: string) => bind (stringmany identrest) (fun tl =>
            eval (string.append hd tl) `return JSON.stringify(JSON.parse(${hd}) + JSON.parse(${tl}));`))).

cterm : type.
clam : string -> cterm -> cterm.
cvar : string -> cterm.
capp : cterm -> list cterm -> cterm.

cterm, cterm0, cterm1, cterm2 : peg cterm.

rule cterm cterm0.

rule cterm0 (seq (token (exact "λ")) (bind (token ident) (fun var => seq (token (exact "."))
                 (bind cterm0 (fun body => action (clam var body)))))).

rule cterm0 cterm1.

rule cterm1 (bind cterm2 (fun hd => bind (once_or_many (seq ws cterm2)) (fun tl => action (capp hd tl)))).

rule cterm1 cterm2.

rule cterm2 (bind (token ident) (fun var => action (cvar var))).

rule cterm2 (seq (token (exact "(")) (bind cterm0 (fun t => seq (token (exact ")")) (action t)))).

>> parse cterm "λ x.x" Res ?
>> Yes:
>> Res := (clam "x" (cvar "x"), "").

>> parse cterm "λf.λg.λx.f (g x)" Res ?
>> Yes:
>> Res := ((clam "f" (clam "g" (clam "x" (capp (cvar "f") [capp (cvar "g") [cvar "x"]])))), "").

>> parse ident "kalimera" Res ?
>> Yes:
>> Res := ("kalimera", "").

>> parse cterm "λ καλημέρα. κόσμε" Res ?
>> Yes:
>> Res := (clam "καλημέρα" (cvar "κόσμε"), "").

>> (parse (bind anychar (fun a => bind anychar (fun b => action (fun s => {prop| string.explode s [b, a] |})))) "hi" (_DoIt, Rest), _DoIt S) ?
>> Yes:
>> S := "ih",
>> Rest := "".

>> (parse (bind anychar (fun a => bind anychar (fun b => action (`${b}${a}`)))) "hi" (Exp, Rest), expansion.str Exp S) ?
>> Yes:
>> S := "ih",
>> Rest := "".

>> parse (action (fun (x: string) => x)) "" (X, _) ?
>> Yes:
>> X := (fun (x: string) => x).

>> parse (apply capp [captured cterm2, captured (many cterm2)]) "x y z" (X, _) ?
>> Yes:
>> X := capp (cvar "x") [cvar "y", cvar "z"].

>> parse (transform (pfun x y res => string.append y x res) "(function(a, b) { return JSON.stringify(JSON.parse(b) + JSON.parse(a)); })" [ captured (charclass "a"), captured (charclass "b") ]) "ab" X ?
>> Yes:
>> X := ("ba", "").

>> parse (string_transform (fun a b => expansion.str `${a} : ${b}`) [ captured anychar, captured anychar ]) "xy" (X, _) ?
>> Yes:
>> X := "x : y".

testlam : peg cterm.
hoaslam : (cterm -> cterm) -> cterm.

>> parse (action (hoaslam (fun x => hoaslam (fun y => (capp x [capp y [cvar "y"]]))))) "" (X, _) ?
>> Yes:
>> X := hoaslam (fun x => hoaslam (fun y => (capp x [capp y [cvar "y"]]))).

>> (eq _OPEN "λ  hello  . λ world . λ  hello  . λ world . ",
    eq _WHITESPACE "                                                                                   ",
    expansion.str `   hello   ` _BODY,
    expansion.str `${_OPEN} (${_WHITESPACE} ${_OPEN} (${_WHITESPACE} ${_OPEN} (
                   ${_OPEN} (
                   ${_WHITESPACE}
                   ${_WHITESPACE}
                   ${_BODY}) ${_BODY}) ${_BODY}) ${_BODY})` _STR,
    parse cterm _STR _X) ?
>> Yes.

(* use two letrecs to make sure that unique symbols get generated *)
rec : int -> peg cterm.
rule (rec I)
           (letrec (open_next (fun cterm_lambda => open_next (fun cterm_base => open_end
           ([ (* lambda *)
             choices [
               (seq (token (exact "λ")) (bind (token ident) (fun var => seq (token (exact "."))
                     (bind cterm_lambda (fun body => action (clam var body)))))),
               cterm_base
             ],
             (* base *)
             choices [
               bind (token ident) (fun var => action (cvar var)),
               seq (token (exact "(")) (bind cterm_lambda (fun t => seq (token (exact ")")) (action t)))
             ]
          ],
          cterm_lambda))))).

>> parse (choices [ seq (neg (rec 0)) (action (cvar "nope")), rec 1 ]) " λ x . λ y . (λx. x)" (X, _) ?
>> Yes:
>> X := clam "x" (clam "y" (clam "x" (cvar "x"))).

(* Actual bind, with user-defined PEGs *)
>> parse (bind ident (fun id => token (exact id))) "lala lala" X ?
>> Yes:
>> X := ("lala", "").

%end.
