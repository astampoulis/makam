%use layout_grammar.
%open peg.

%extend layout_grammar_tests.

all : testsuite. %testsuite all.

>> parse_opt (layout.enter (seq ws layout.current_line_offset)) "\n  hello" (X, _) ?
>> Yes:
>> X := 2.

>> parse_opt (layout.enter (seq (seq ws (seq (exact "hello") ws)) layout.current_line_offset)) "\n  hello\r   foo" (X, _) ?
>> Yes:
>> X := 3.

>> parse_opt (layout.enter (seq ws_opt (seq (exact "do") (seq layout.begin (seq (once_or_many (charclass "abcdefghijklmnopqrstuvwxyz")) (seq ws layout.current_layout_offset)))))) {
     do foo
        bar
   } (X, _) ?
>> Yes:
>> X := 8.

rosetree : type.
rosetree_aux, rosetree : peg rosetree.
rosetree : string -> list rosetree -> rosetree.

label : peg string.
rule label (syntax (syntax.char_once_or_many (syntax.charclass "abcdefghijklmnopqrstuvwxyz"))).

token : peg A -> peg A.
rule (token S) (seq (option layout.ws) (bind S (fun res => seq (option layout.ws) (action res)))).
inline (token S).

token : string -> peg string.
rule (token (S: string)) (seq (option layout.ws) (bind (exact S) (fun res => seq (option layout.ws) (action res)))).
inline (token S).

rule rosetree_aux (
  apply rosetree [
    captured (token label),
    captured (choices [layout.block (fun sep => many (seq sep rosetree_aux)), action []])
  ]).
rule rosetree (apply (fun x => x) [captured (layout.enter (layout.block (fun _ => rosetree_aux))), ws_opt]).

`( def_toplevel_parser_js rosetree ).

>> parse_opt rosetree {
 foo bar
     baz
 } X ?
>> Yes:
>> X := (rosetree "foo" [ rosetree "bar" [], rosetree "baz" [] ], "").

>> parse_opt rosetree {
 foo
   bar
     roo
     noo
   baz
     roo
     noo
 } X ?
>> Yes:
>> X := (rosetree "foo" [ rosetree "bar" [ rosetree "roo" [], rosetree "noo" [] ], rosetree "baz" [ rosetree "roo" [], rosetree "noo" [] ] ], "").

>> parse_opt rosetree {
 foo bar baz
     roo noo
     baa  daa
          naa
 } X ?
>> Yes:
>> X := (rosetree "foo" [ rosetree "bar" [ rosetree "baz" [] ], rosetree "roo" [ rosetree "noo" [] ], rosetree "baa" [ rosetree "daa" [], rosetree "naa" [] ] ], "").

>> parse_opt rosetree {
 foo bar} X ?
>> Yes:
>> X := (rosetree "foo" [ rosetree "bar" [] ], "").

>> parse_opt rosetree {
 foo


   bar

   baz} X ?
>> Yes:
>> X := (rosetree "foo" [ rosetree "bar" [], rosetree "baz" [] ], "").

>> parse_opt rosetree {
    foo
 lala
} X ?
>> Yes:
>> X := (rosetree "foo" [ ], "lala\n").


(* let-in's as in Haskell *)

term : type.
letin : list (string * term) -> term -> term.
const : string -> term.

ptermtop : peg term.
pterm : peg term.

rule pterm (apply letin [
  token "'let",
  captured (layout.block (fun sep => many (apply tuple [
    sep,
    captured (token label),
    token "=",
    captured pterm
  ]))),
  token "'in",
  captured (layout.block (fun _ => pterm))
]).

rule pterm (apply const [ captured (token label) ]).

rule ptermtop (seq ws_opt (layout.enter pterm)).

`( def_toplevel_parser_js ptermtop ).

>> parse_opt ptermtop {{
'let x = hello
     y = hullo
'in x
}}
X ?
>> Yes:
>> X := (letin [ ("x", const "hello"), ("y", const "hullo") ] (const "x"), "").

>> parse_opt ptermtop {{
'let x = hello
     y = hullo
'in
x
}}
X ?
>> Yes:
>> X := (letin [ ("x", const "hello"), ("y", const "hullo") ] (const "x"), "").

>> parse_opt ptermtop {{
'let x = hello
     y = hullo
'in
 x
}}
X ?
>> Yes:
>> X := (letin [ ("x", const "hello"), ("y", const "hullo") ] (const "x"), "").

>> parse_opt ptermtop {{
'let
   x = hello
   y = hullo
'in x
}}
X ?
>> Yes:
>> X := (letin [ ("x", const "hello"), ("y", const "hullo") ] (const "x"), "").

>> parse_opt ptermtop {{
'let x = hello
     y = hullo 'in x
}}
X ?
>> Yes:
>> X := (letin [ ("x", const "hello"), ("y", const "hullo") ] (const "x"), "").


%end.
